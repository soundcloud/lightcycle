buildscript {
  repositories {
    mavenLocal()
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.0'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.0'
  }
}

allprojects {
  repositories {
    mavenLocal()
    google()
    jcenter()
  }
}

ext {
  minSdkVersion = 21
  targetSdkVersion = 27
  compileSdkVersion = 29
  sourceCompatibilityVersion = JavaVersion.VERSION_1_8
  targetCompatibilityVersion = JavaVersion.VERSION_1_8
}

ext.deps = [
  // Android
  android               : "com.google.android:android:4.1.1.4",
  preference            : "androidx.preference:preference:1.1.0",
  appcompat             : "androidx.appcompat:appcompat:1.1.0",
  annotations           : "androidx.annotation:annotation:1.1.0",

  // Processor
  javapoet              : 'com.squareup:javapoet:1.6.1',

  // Test dependencies
  junit                 : 'junit:junit:4.12',
  mockito               : 'org.mockito:mockito-all:1.10.19',
  compiletesting        : 'com.google.testing.compile:compile-testing:0.18',
  roboelectric          : 'org.robolectric:robolectric:4.3.1',
  equalsverifier        : 'nl.jqno.equalsverifier:equalsverifier:2.1.8',
  fragment_testing      : 'androidx.fragment:fragment-testing:1.2.0',
  androidx_junit        : 'androidx.test.ext:junit:1.1.1'
]

// Static analysis
subprojects { project ->
  apply plugin: 'checkstyle'

  checkstyle {
    toolVersion = "7.2"
    configFile rootProject.file('checkstyle.xml')
  }

  task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }
}

// configure Java projects
[":lightcycle-api", ":lightcycle-processor"].each { name ->
  project(name) {

    apply plugin: 'provided-base'
    apply plugin: 'java'

    sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
    targetCompatibility = rootProject.ext.targetCompatibilityVersion
  }
}
