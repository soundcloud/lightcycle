buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.0'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.0'
  }
}

task wrapper(type: Wrapper) {
  description 'Creates the gradle wrapper.'
  gradleVersion '2.5'
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

ext {
  minSdkVersion = 8
  targetSdkVersion = 23
  compileSdkVersion = 23
  buildToolsVersion = '23.0.1'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

def supportLibVersion = '23.1.1'

ext.deps = [
  // Android
  android               : 'com.google.android:android:4.1.1.4',
  support_v4            : 'com.google.android:support-v4:r7',
  preference_v7         : "com.android.support:preference-v7:${supportLibVersion}",
  appcompat_v7          : "com.android.support:appcompat-v7:${supportLibVersion}",
  support_annotations   : "com.android.support:support-annotations:${supportLibVersion}",
  // Processor
  javapoet              : 'com.squareup:javapoet:1.6.1',

  // Test dependencies
  junit                 : 'junit:junit:4.12',
  mockito               : 'org.mockito:mockito-all:1.10.19',
  compiletesting        : 'com.google.testing.compile:compile-testing:0.7',
]

// Static analysis
subprojects { project ->
  apply plugin: 'pmd'
  apply plugin: 'findbugs'

  task pmd(type: Pmd) {
    description 'Finds common programming flaws throw static analysis of code.'
    ignoreFailures false
  }

  findbugs {
    ignoreFailures = false
    toolVersion = "3.0.0"
  }

  tasks.withType(FindBugs) {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }
}

// configure Java projects
[":lightcycle-api", ":lightcycle-processor"].each { name ->
  project(name) {

    apply plugin: 'provided-base'
    apply plugin: 'java'

    sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
    targetCompatibility = rootProject.ext.targetCompatibilityVersion
  }
}
